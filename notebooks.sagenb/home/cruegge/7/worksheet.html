

<h2>Aufgabe 1</h2>
<p>Bestimmen Sie den Fl&auml;cheninhalt des Dreiecks, welches durch folgende Vektoren beschrieben wird: \[v_1=\left(\begin{array}{c} 5\\-3\\4 \end{array}\right),\quad v_2=\left(\begin{array}{c} 7\\6\\8 \end{array}\right),\quad v_3=\left(\begin{array}{c} 9\\2\\-13 \end{array}\right)\]&nbsp;Stellen Sie dieses Dreieck graphisch dar.</p>

{{{id=1|
reset()
v1 = vector([5,-3,4])
v2 = vector([7,6,8])
v3 = vector([9,2,-13])
///
}}}

{{{id=4|
norm( (v2-v1).cross_product(v3-v1) )/2
///
1/2*sqrt(33105)
}}}

{{{id=5|
n(norm( (v2-v1).cross_product(v3-v1) )/2)
///
90.9738973552304
}}}

{{{id=9|
polygon3d([v1,v2,v3])
///
}}}

<h2>Aufgabe 2</h2>
<p>Plotten Sie die Funktion $f:(x,y) \mapsto 0.1y^4 - 0.3y^3 - 1.5y^2 + \sin(3x) +1.9y +3$ als Fl&auml;che im dreidimensionalen Raum. Betrachten Sie ebenfalls die Konturen bei -2, 0, 2, 4 und 6. W&auml;hlen Sie jeweils geeignete Definitionsbereiche.</p>

{{{id=10|
reset()
var('x,y')
///
(x, y)
}}}

{{{id=12|
f(x,y) = 0.1*y^4 - 0.3*y^3 - 1.5*y^2 + sin(3*x) + 1.9*y + 3
///
}}}

{{{id=13|
plot3d(f(x,y), (x,-4,4), (y,-4,4))
///
}}}

{{{id=14|
contour_plot(f(x,y), (x,-4,4), (y,-4,4), contours=[-2,0,2,4,6], fill=False, labels=True)
///
<html><font color='black'><img src='cell://sage0.png'></font></html>
}}}

<h2>Aufgabe 3 *</h2>
<p>Berechnen Sie die ersten 4 Glieder der Taylor-Reihe der Funktionen $f(x)=(1-x)^{-1}$ und $g(x)=(1+x+x^2+x^3+x^4+x^5)^{-1}$ an der Stelle x=3. Berechnen Sie die Entwicklung erst "per Hand" in Sage und &uuml;berpr&uuml;fen Sie dann Ihr Ergebnis mit der Funktion <strong>taylor</strong>.</p>

{{{id=15|
reset();forget()
///
}}}

{{{id=82|
var('x')
f = 1/(1-x)
g = 1/(1+x+x^2+x^3+x^4+x^5)
///
}}}

{{{id=78|
t = 0
for n in [0..3]:
    t = t + diff(f,x,n)(x=3) / n.factorial() * (x-3)^n
t
///
1/16*(x - 3)^3 - 1/8*(x - 3)^2 + 1/4*x - 5/4
}}}

{{{id=24|
# oder:
sum([diff(f,x,n)(x=3)/n.factorial()*(x-3)^n for n in [0..3]])
///
1/16*(x - 3)^3 - 1/8*(x - 3)^2 + 1/4*x - 5/4
}}}

{{{id=79|
#Vorsicht! das geht nicht! Faustregel: diese sum nur bei symbolischen Ausdrücken benutzen
_=var('n');sum( (diff(f,x,n))(x=3)/n.factorial()*(x-3)^n,n,0,3)
///
0
}}}

{{{id=25|
taylor(f,x,3,3)
///
1/16*(x - 3)^3 - 1/8*(x - 3)^2 + 1/4*x - 5/4
}}}

{{{id=26|
sum([diff(g,x,n)(x=3)/n.factorial()*(x-3)^n for n in [0..3]])
///
-44310267/17555190016*(x - 3)^3 + 177633/48228544*(x - 3)^2 - 547/132496*x + 2005/132496
}}}

{{{id=27|
taylor(g,x,3,3)
///
-44310267/17555190016*(x - 3)^3 + 177633/48228544*(x - 3)^2 - 547/132496*x + 2005/132496
}}}

<h2>Aufgabe 4</h2>
<p>Zeigen Sie, dass die Funktion $f:\mathbb{R}_{+} \to \mathbb{R}$, $f(x)=x e^{-nx}$, $n&gt;0$ genau ein Maximum an der Stelle $x=1/n$ besitzt. Zeigen Sie, dass das Maximum global ist.</p>

{{{id=29|
reset();forget()
var('x,n')
f(x) = x*exp(-n*x)
///
}}}

{{{id=30|
solve(diff(f,x)==0, x)
///
[x == (1/n)]
}}}

{{{id=75|
forget()
assume(n>0)
print diff(f,x,2)(1/n)
print bool(diff(f,x,2)(1/n) < 0)
///
-n*e^(-1)
True
}}}

{{{id=77|
f(0), f(1/n), f.limit(x=oo)
///
(0, e^(-1)/n, 0)
}}}

<h2>Aufgabe 5</h2>
<p>Zeigen Sie mit der Regel von L'Hospital folgende Grenzwerte:</p>
<p>\[\lim_{x\to\frac{\pi}{2}} \frac{\cos(x)}{\frac{\pi}{2}-x} = 1,\]</p>
<p>\[\lim_{x\to0}\left(\frac{1}{\sin(x)} - \frac{1}{x}\right) = 0.\]</p>

{{{id=41|
reset()
var('x')
///
x
}}}

{{{id=49|
(cos(x)).limit(x=pi/2), (pi/2-x).limit(x=pi/2)
///
(0, 0)
}}}

{{{id=45|
( diff(cos(x),x) / diff(pi/2-x,x) ).limit(x=pi/2)
///
1
}}}

{{{id=93|
limit( cos(x)/(pi/2-x), x=pi/2 )
///
1
}}}

{{{id=50|
(x-sin(x)).limit(x=0), (x*sin(x)).limit(x=0)
///
(0, 0)
}}}

{{{id=47|
( diff(x-sin(x),x) / diff(x*sin(x),x) ).limit(x=0)
///
0
}}}

<h2>Aufgabe 6</h2>
<p>Pr&uuml;fen Sie folgende Funktionen auf Differenzierbarkeit:</p>
<p>\[f(x) = \left\{\begin{array}{ll} \sqrt{x}, &amp; x\geq1 \\ x^3+2.5x-2, &amp; x&lt;1 \end{array}\right., \qquad g(x) = \left\{\begin{array}{ll} x^2, &amp; x\geq0 \\ -x, &amp; x&lt;0 \end{array}\right..\]</p>

{{{id=51|
reset()
var('x')
///
x
}}}

{{{id=48|
limit(sqrt(x), x=1, dir='plus'), limit(x^3+2.5*x-2, x=1, dir='minus')
///
(1, 1.5)
}}}

{{{id=53|
limit(x^2, x=0, dir='plus'), limit(-x, x=0, dir='minus')
///
(0, 0)
}}}

{{{id=54|
limit(diff(x^2,x), x=0, dir='plus'), limit(diff(-x,x), x=0, dir='minus')
///
(0, -1)
}}}

<h2>Aufgabe 7</h2>
<ol>
<li>Differenzieren Sie folgende Funktionen: \[f(x)=\exp\left(\frac{\frac{\sin 2x}{\sin x}}{\left(\frac{\log8}{2+2\cos x}\right)^{-1}-\frac{8}{\log x^4}}\right)\quad\mathrm{und}\quad g(x)=\sin\left(2^{\cos 3x}\right).\]</li>
<li>Berechnen Sie \[4\int_0^1 \frac{dx}{1+x^2},\quad \int_0^\pi e^x \sin x\,dx\quad\mathrm{und}\quad \int\frac{1}{e^x+e^{-x}} dx.\]</li>
<li>Berechnen Sie die 20. Ableitung von $f(x) = \sin(2x)\exp(x)$.</li>
</ol>
<p>&nbsp;</p>

{{{id=56|
reset()
var('x')
///
x
}}}

{{{id=55|
show( diff(exp(sin(2*x)/sin(x)/( (ln(8)/(2+2*cos(x)))^(-1) - 8/ln(x^4) )), x) )
///
<html><div class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\frac{1}{2} \, {\left(\frac{{\left(\frac{\sin\left(x\right)}{\log\left(8\right)} - \frac{16}{x \log\left(x^{4}\right)^{2}}\right)} \sin\left(2 \, x\right)}{{\left(\frac{\cos\left(x\right) + 1}{\log\left(8\right)} - \frac{4}{\log\left(x^{4}\right)}\right)}^{2} \sin\left(x\right)} - \frac{\sin\left(2 \, x\right) \cos\left(x\right)}{{\left(\frac{\cos\left(x\right) + 1}{\log\left(8\right)} - \frac{4}{\log\left(x^{4}\right)}\right)} \sin\left(x\right)^{2}} + \frac{2 \, \cos\left(2 \, x\right)}{{\left(\frac{\cos\left(x\right) + 1}{\log\left(8\right)} - \frac{4}{\log\left(x^{4}\right)}\right)} \sin\left(x\right)}\right)} e^{\left(\frac{\sin\left(2 \, x\right)}{2 \, {\left(\frac{\cos\left(x\right) + 1}{\log\left(8\right)} - \frac{4}{\log\left(x^{4}\right)}\right)} \sin\left(x\right)}\right)}</div></html>
}}}

{{{id=58|
show( diff(sin(2^(cos(3*x))), x) )
///
<html><div class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-3 \, 2^{\cos\left(3 \, x\right)} \log\left(2\right) \sin\left(3 \, x\right) \cos\left(2^{\cos\left(3 \, x\right)}\right)</div></html>
}}}

{{{id=59|
4*integral(1/(1+x^2), x,0,1)
///
pi
}}}

{{{id=60|
integral(sin(x)*exp(x), x,0,pi)
///
1/2*e^pi + 1/2
}}}

{{{id=61|
integral(1/(exp(x)+exp(-x)), x)
///
arctan(e^x)
}}}

{{{id=62|
diff(sin(2*x)*exp(x), x,20)
///
-9653287*e^x*sin(2*x) - 1476984*e^x*cos(2*x)
}}}

<h2>Aufgabe 8 *</h2>
<p>Schreiben Sie eine Prozedur, die eine Nullstelle einer gegebenen Funktion $f:[a_0,\,b_0]\to\mathbb{R}$ mit $f(a_0)\cdot f(b_0)&lt;0$ zu einer gegebenen Genauigkeit $\mathrm{TOL}$ berechnet. Betrachten Sie hierzu eine Folge von Intervallen $[a_k,\,b_k]$, die sich aus der folgenden Vorschrift ergeben:</p>
<p>Wiederhole bis $|b_k - a_k| \leq 2*\mathrm{TOL}$:</p>
<ul>
<li>Berechne $g:=f\left(\frac{a_k+b_k}{2}\right)$.</li>
<li>Gilt $f(a_k) * g&lt;0$, so definiere $a_{k+1}=a_k,\,b_{k+1}=\frac{a_k+b_k}{2}$.</li>
<li>Andernfalls definiere $a_{k+1}=\frac{a_k+b_k}{2},\,b_{k+1}=b_k$.</li>
</ul>
<p>Testen Sie die Prozedur an $f(x)=x^3-2$, $[a_0, b_0] = [0,3]$ f&uuml;r $\mathrm{TOL}=10^{-4}$ und $\mathrm{TOL}=10^{-6}$. Verwenden Sie maximal 100 Iterationen.</p>
<ul>
</ul>

{{{id=66|
reset()
var('x')
///
x
}}}

{{{id=63|
def nullstelle(f, a0, b0, TOL):
    ak = a0
    bk = b0
   
    # Die Bedingung abs(b0-a0) > 2^(k+1)*TOL ist äquivalent
    # zu abs(bk-ak) > 2*TOL
    
    while abs(bk-ak) > 2*TOL:
        g = f((ak+bk)/2)
        if f(ak)*g < 0:
            bk = (ak+bk)/2
        else:
            ak = (ak+bk)/ 2
            
    # Wenn die Schleife abbricht ist die Nullstelle weniger
    # als TOL vom Intervall-Mittelpunkt entfernt.
    return (ak+bk)/2
///
}}}

{{{id=65|
f(x) = x^3 - 2
n(2^(1/3))
///
1.25992104989487
}}}

{{{id=67|
n(nullstelle(f, 0,3, 10^(-4)))
///
1.25985717773438
}}}

{{{id=68|
n(nullstelle(f, 0,3, 10^(-6)) )
///
1.25992083549500
}}}

{{{id=87|
# Alternative - Rekursive Funktion
def nullstelle_rek(f, a, b, TOL):
    if abs(b-a) > 2*TOL:
        if f(a)*f((a+b)/2) < 0:
            return nullstelle_rek(f,a,(a+b)/2,TOL)
        else:
            return nullstelle_rek(f,(a+b)/2,b,TOL)
    return (a+b)/2
///
}}}

{{{id=88|
n(nullstelle_rek(f, 0,3, 10^(-4)))
///
1.25985717773438
}}}

{{{id=94|

///
}}}
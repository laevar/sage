

<h2>Aufgabe 1 *</h2>

<p>F&uuml;hren Sie eine Kurvendiskussion durch f&uuml;r die Funktion<br />\[&nbsp; f: x \quad \mapsto \quad \exp(1/x)+\frac{1}{4}x\exp(1/x)\]</p>
<ul>
<li>Untersuchen Sie das Verhalten von $f$ bei der Polstelle $x=0$.</li>
<li>Untersuchen Sei das Verhalten von $f$ f&uuml;r $x \Rightarrow \pm \infty$.</li>
<li>Berechnen Sie Nullstellen, Extremstellen und Wendepunkte. &Uuml;berlegen Sie sich, ob lokale/globale Maxima oder Minima an den Extremstellen vorliegen und geben Sie sie an.&nbsp;</li>
<li>Plotten Sie den Graphen&nbsp; auf dem Intervall $[-5,10]$.</li>
</ul>

{{{id=4|
reset()
f(x) = exp(1/x)+1/4*x*exp(1/x) ; f
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}x \ {\mapsto}\ \frac{1}{4} \, x e^{\frac{1}{x}} + e^{\frac{1}{x}}</span></html>
}}}

{{{id=15|
f.limit(x=0,dir='minus'),f.limit(x=0,dir='plus')
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left(0, +\infty\right)</span></html>
}}}

{{{id=1|
f.limit(x=oo);f.limit(x=-oo)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}+\infty</span></html>
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-\infty</span></html>
}}}

{{{id=5|
solve(f==0,x)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left[x = \left(-4\right), e^{\frac{1}{x}} = 0\right]</span></html>
}}}

{{{id=6|
fprime = f.differentiate(); print fprime
fdprime = fprime.differentiate(); print fdprime
///
x |--> -1/4*e^(1/x)/x - e^(1/x)/x^2 + 1/4*e^(1/x)
x |--> 9/4*e^(1/x)/x^3 + e^(1/x)/x^4
}}}

{{{id=7|
ES = solve(fprime==0,x); ES
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left[x = -\frac{1}{2} \, \sqrt{17} + \frac{1}{2}, x = \frac{1}{2} \, \sqrt{17} + \frac{1}{2}, e^{\frac{1}{x}} = 0\right]</span></html>
}}}

{{{id=9|
ES[1].rhs(); float(ES[1].rhs());float(fdprime(ES[1].rhs()));
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\frac{1}{2} \, \sqrt{17} + \frac{1}{2}</span></html>
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}2.56155281281</span></html>
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}0.232113971819</span></html>
}}}

{{{id=10|
ES[0].rhs(); float(ES[0].rhs());float(fdprime(ES[0].rhs()));
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-\frac{1}{2} \, \sqrt{17} + \frac{1}{2}</span></html>
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-1.56155281281</span></html>
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-0.222809678778</span></html>
}}}

{{{id=17|
WP = solve(fdprime==0,x); WP
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left[x = \left(-\frac{4}{9}\right), e^{\frac{1}{x}} = 0\right]</span></html>
}}}

{{{id=18|
float((f.differentiate())(WP[0].rhs()))
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-0.447946704388</span></html>
}}}

{{{id=11|
plot(f,xmin=-5,xmax=10,ymin=-1,ymax=4)
///
<html><font color='black'><img src='cell://sage0.png'></font></html>
}}}

<h2>Aufgabe 2</h2>

<p>Definieren Sie die Funktion <br />\[ f(s):= \int_0^\infty x^{s-1} e^{-x} dx, \quad s &gt;0 \]<br />und berechnen Sie $f(1), f(2), \dots ,f(10)$. <br />Erkennen Sie einen Zusammenhang zu einer klassischen Funktion? Berechnen Sie $\left( 8<br />f(2) f(5/2)/f(4) \right)^2$!</p>

{{{id=40|
# Definition als Python-Funktion ("black box")
reset()
def g(s): return integral(x^(s-1)*exp(-x),x,0,oo)
g(2)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}1</span></html>
}}}

{{{id=48|
# oder als symbolischer Ausdruck:
reset(); forget()
var('s')
assume(s>0)
g(s) = integral(x^(s-1)*exp(-x),x,0,oo)
g
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}s \ {\mapsto}\ \Gamma\left(s\right)</span></html>
}}}

{{{id=22|
[g(m) for m in range(1,11) ]
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left[1, 1, 2, 6, 24, 120, 720, 5040, 40320, 362880\right]</span></html>
}}}

{{{id=23|
(8*g(2)*g(5/2)/g(4))^2
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\pi</span></html>
}}}

<h2>Aufgabe 3</h2>

<p>Versuchen&nbsp; Sie, die folgenden Grenzwerte zu erraten, und<br />&nbsp; berechnen Sie sie dann<br />\[ \lim_{x \rightarrow \infty} \left( 1 + \frac{a}{x} \right)^x, \quad <br />\lim_{x \rightarrow 0} x\sin(\frac{1}{x}) , \quad \lim_{x \rightarrow<br />&nbsp; 0} \sin(\frac{1}{x}).\]</p>

{{{id=24|
reset()
var('a')
limit((1+a/x)^x,x=oo)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}e^{a}</span></html>
}}}

{{{id=25|
limit(sin(1/x)*x,x=0)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}0</span></html>
}}}

{{{id=26|
limit(sin(1/x),x=0)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\mbox{ind}</span></html>
}}}

<h2>Aufgabe 4</h2>

<p>Bestimmen Sie die folgenden unbestimmten Integrale<br />\[&nbsp; \int \frac{x}{ \sqrt{(2ax-x^2)^3}} dx, \qquad \int \frac{1}{x<br />&nbsp;&nbsp;&nbsp; \sqrt{1+x^2}} dx. \]</p>

{{{id=43|
reset()
var('a')
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}a</span></html>
}}}

{{{id=27|
integral(x/(2*a*x-x^2)^(3/2),x)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\frac{x}{\sqrt{2 \, a x - x^{2}} a}</span></html>
}}}

{{{id=28|
integral(1/(x*sqrt(1+x^2)),x)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-{\rm arcsinh}\left(\frac{1}{{\left| x \right|}}\right)</span></html>
}}}

<h2>Aufgabe 5</h2>

<p>Verifizieren sie mit Sage die Identit&auml;t <br />\[ x^{(n)} y^{(n)}-(x y)^{(n)}=0 \]<br />f&uuml;r $n\in \mathbb{N}$.</p>
<p><strong>Hinweis:</strong> Benutzen sie <em>assume(n,'integer')</em> f&uuml;r $n\in \mathbb{N}$</p>

{{{id=29|
reset(); forget()
var('n,x,y')
f = x^(n)*y^(n)-(x*y)^(n)
assume(n, 'integer')
print f.simplify()
bool(f == 0)
///
0
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}{\rm True}</span></html>
}}}

<h2>Aufgabe 6</h2>

<p>Faktorisieren Sie $2x^2-2a^2-x^3-2x^4+x^5+a^2x+2a^2x^2-a^2x^3$.</p>

{{{id=30|
reset()
var('x,a')
factor(2*x^2-2*a^2-x^3-2*x^4+x^5+a^2*x+2*a^2*x^2-a^2*x^3)
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}-{\left(x - 2\right)} {\left(x - 1\right)} {\left(x + 1\right)} {\left(a - x\right)} {\left(a + x\right)}</span></html>
}}}

<h2>Aufgabe 7*</h2>

<p>Zeichnen und berechnen Sie den Schnittpunkt der Ebene <br />\[ E: \vec{x}= <br />\left ( \begin{array}{c}&nbsp; 2 \\ 1 \\ -1 \end{array} \right) +l <br />\left ( \begin{array}{c}&nbsp; 1 \\ -1 \\ 1 \end{array} \right) +m<br />\left ( \begin{array}{c}&nbsp; -3 \\ 1 \\ 4 \end{array} \right), \quad l,m<br />\in \mathbb{R}<br />\]<br />mit der Geraden <br />\[<br />g: \vec{x}=<br />\left ( \begin{array}{c}&nbsp; 3 \\ 4 \\ 5 \end{array} \right) +k<br />\left ( \begin{array}{c}&nbsp; 4 \\ -1 \\ 2 \end{array} \right), \quad k \in \mathbb{R}.<br />\]</p>

{{{id=44|
reset()
var('k,l,m')

E0 = vector([2,1,-1])
E1 = vector([1,-1,1])
E2 = vector([-3,1,4])

E = E0 + l*E1 + m*E2

g0 = vector([3,4,5])
g1 = vector([4,-1,2])

g = g0 + k*g1
///
}}}

{{{id=49|
klm = solve(E - g, [k,l,m], solution_dict=true); klm
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left[\left\{m:\: \frac{23}{17}, l:\: -\frac{66}{17}, k:\: -\frac{38}{17}\right\}\right]</span></html>
}}}

{{{id=50|
schnittpunkt = g.subs(klm[0]); schnittpunkt
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left(-\frac{101}{17},\frac{106}{17},\frac{9}{17}\right)</span></html>
}}}

{{{id=45|
p = parametric_plot3d(g, (k,-4,0), thickness=3)
p += parametric_plot3d(E, (l,-6,-2), (m,0,4), color='green', opacity=0.8)
p.show()
///
}}}

<h2>Aufgabe 8</h2>

{{{id=35|
reset()
menge = [1..1000]
///
}}}

{{{id=38|
[m for m in menge if (mod(m,3)==0) and (mod(m,2)==0) and (mod(m,7) ==0)]
///
<html><span class="math">\newcommand{\Bold}[1]{\mathbf{#1}}\left[42, 84, 126, 168, 210, 252, 294, 336, 378, 420, 462, 504, 546, 588, 630, 672, 714, 756, 798, 840, 882, 924, 966\right]</span></html>
}}}